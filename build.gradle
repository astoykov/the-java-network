import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'war'
    id "com.bmuschko.tomcat" version "2.2.2"
    id 'io.spring.dependency-management' version '0.5.1.RELEASE'
    id 'org.flywaydb.flyway' version '3.2.1'
}

ext {
    libraryVersions = [
            // Core Dependencies - Spring
            orgSpringframeworkVersion     : '4.1.6.RELEASE',
            // Core Dependencies - Others
            orgSlf4jVersion               : '1.7.12',
            log4jVersion                  : '1.2.17',
            orgAspectjVersion             : '1.8.5',
            comFasterxmlJacksonCoreVersion: '2.5.3',
            comJaywayJsonpathVersion      : '2.0.0'
    ]
}

group = 'com.alesto'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

/*
 * ======================================================================
 * ----- Filter properties
 *
 * To add new property:
 * 1. Add the property and the value in "PROJECT/gradle.properties" (can be overridden in GRADLE_USER_HOME/gradle.properties. Default to: ~/.gradle/gradle.properties)
 * 2. Add the property alias in filterProperties variable
 * 3. Add the property alias in src/main/resources/properties/application.properties
 * ======================================================================
 */

def filterProperties = [
        'dbMysqlHost'    : project.property('dbMysqlHost'),
        'dbMysqlPort'    : project.property('dbMysqlPort'),
        'dbMysqlDatabase': project.property('dbMysqlDatabase'),
        'dbMysqlUsername': project.property('dbMysqlUsername'),
        'dbMysqlPassword': project.property('dbMysqlPassword')
]

/*
 * ======================================================================
 * ----- Resources filtering
 * ======================================================================
 */

processResources {
    filter ReplaceTokens, tokens: filterProperties
}

/*
 * ======================================================================
 * ----- DB migrations
 * ======================================================================
 */

flyway {
    driver = 'com.mysql.jdbc.Driver'
    table = 'schema_version'
    locations = ['db/migration']
    encoding = 'UTF-8'
    validateOnMigrate = 'true'
}

/*
 * ======================================================================
 * ----- Dependencies
 * ======================================================================
 */

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/release/' }
    maven { url 'http://repo.spring.io/milestone/' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework:spring-framework-bom:' + libraryVersions.orgSpringframeworkVersion
    }
}

dependencies {
    def tomcatVersion = '7.0.62'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"

    // Core Dependencies - Java
    compile group: 'javax.inject', name: 'javax.inject', version: '1'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'javax.servlet', name: 'jstl', version: '1.2'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.35'
    // Core Dependencies - Container
    // CORS support: https://issues.apache.org/bugzilla/show_bug.cgi?id=55046 (Tomcat >= 7.0.41)
    providedCompile group: 'org.apache.tomcat', name: 'tomcat-catalina', version: '8.0.22'
    // Core Dependencies - Logging
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: libraryVersions.orgSlf4jVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: libraryVersions.orgSlf4jVersion
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: libraryVersions.orgSlf4jVersion
    compile group: 'log4j', name: 'log4j', version: libraryVersions.log4jVersion
    // Core Dependencies - Tools
    compile group: 'joda-time', name: 'joda-time', version: '2.7'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3.6'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.8.2'
    compile group: 'org.atteo', name: 'evo-inflector', version: '1.2.1'
    // Core Dependencies - Testing
    testCompile group: 'javax', name: 'javaee-api', version: '7.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: libraryVersions.comJaywayJsonpathVersion
    // Core Dependencies - aspectj
    compile group: 'org.aspectj', name: 'aspectjrt', version: libraryVersions.orgAspectjVersion
    compile group: 'org.aspectj', name: 'aspectjweaver', version: libraryVersions.orgAspectjVersion
    // Core Dependencies - JSON, XML, RSS & ATOM
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: libraryVersions.comFasterxmlJacksonCoreVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: libraryVersions.comFasterxmlJacksonCoreVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: libraryVersions.comFasterxmlJacksonCoreVersion
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: libraryVersions.comJaywayJsonpathVersion
    compile group: 'rome', name: 'rome', version: '1.0'
    // Core Dependencies - Bean Validation and Mapping
    compile group: 'org.hibernate', name: 'hibernate-validator', version: '5.1.3.Final'
    compile group: 'org.modelmapper', name: 'modelmapper', version: '0.7.4'
    // Core Dependencies - Infrastructure
    compile group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '4.3.9.Final'
    compile group: 'com.h2database', name: 'h2', version: '1.4.187'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '3.2.1'
    // Core Dependencies - Spring
    compile(group: 'org.springframework', name: 'spring-context') {
        exclude(module: 'commons-logging')
    }
    compile group: 'org.springframework', name: 'spring-context-support'
    compile group: 'org.springframework', name: 'spring-tx'
    compile group: 'org.springframework', name: 'spring-orm'
    compile group: 'org.springframework', name: 'spring-oxm'
    compile group: 'org.springframework', name: 'spring-webmvc'
    testCompile group: 'org.springframework', name: 'spring-test'
    compile group: 'org.springframework.plugin', name: 'spring-plugin-core', version: '1.2.0.RELEASE'
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.8.0.RELEASE'
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.17.0.RELEASE'
    compile 'org.springframework.shell:spring-shell:1.1.0.RELEASE'
    subprojects {
        compile "org.springframework.shell:spring-shell:1.1.0.RELEASE"
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: libraryVersions.comFasterxmlJacksonCoreVersion
        compile 'org.springframework.shell:spring-shell:1.1.0.RELEASE'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
